// Configure build
//----------------------------------------------------------------------------------------------------------------------

buildscript {
    ext.orchidVersion = '0.7.9'
    ext.orchidPluginVersion = 'v0.4.0'
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath "gradle.plugin.com.eden.orchidPlugin:buildSrc:${orchidPluginVersion}"
    }
}
apply plugin: "java"
apply plugin: "jacoco"
apply plugin: "idea"
apply plugin: "com.eden.orchidPlugin"
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

// Configure Project
//----------------------------------------------------------------------------------------------------------------------

group 'com.eden'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    jcenter()
    maven { url 'https://dl.bintray.com/javaeden/Orchid/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    compile 'org.fusesource.jansi:jansi:1.14'

    orchidCompile "io.github.javaeden.orchid:OrchidCore:${orchidVersion}"
    orchidCompile "io.github.javaeden.orchid:OrchidBsDoc:${orchidVersion}"
    orchidCompile "io.github.javaeden.orchid:OrchidJavadoc:${orchidVersion}"
    orchidCompile "io.github.javaeden.orchid:OrchidPages:${orchidVersion}"
    orchidCompile "io.github.javaeden.orchid:OrchidWiki:${orchidVersion}"
    orchidCompile "io.github.javaeden.orchid:OrchidChangelog:${orchidVersion}"
}

// Javadoc and Orchid
//----------------------------------------------------------------------------------------------------------------------

orchid {
    version = "${project.version}"
    theme = "BsDoc"

    if(System.getenv('JITPACK')) {
        baseUrl = "https://jitpack.io/com/github/JavaEden/Clog/${System.getenv('VERSION')}/javadoc"
    }
    else if(project.hasProperty('env') && project.property('env') == 'prod') {
        baseUrl = "https://javaeden.github.io/Clog"
    }
    else {
        baseUrl = "http://localhost:8080"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from project.sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

// Jacoco Code Coverage
//----------------------------------------------------------------------------------------------------------------------

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport

// Get release info
//----------------------------------------------------------------------------------------------------------------------

tag {
    message { "Bump version to ${version}" }
}

task getReleaseName {
    doLast {
        println project.version.toString()
    }
}

task getReleaseNotes {
    doLast {
        def versionFilename = project.version.toString().replaceAll("\\.", "_") + ".md"
        def fullVersionFilename = "$projectDir/src/orchid/resources/changelog/$versionFilename"
        def versionFile = file(fullVersionFilename)

        if(versionFile.exists()) {
            println versionFile.text.split("---").last().trim()
        }
        else {
            println "No release notes"
        }
    }
}


// Publish to Bintray
//----------------------------------------------------------------------------------------------------------------------

afterEvaluate { project ->
    def pomConfig = {
        scm {
            url 'https://github.com/JavaEden/Clog.git'
            connection 'https://github.com/JavaEden/Clog.git'
            developerConnection 'https://github.com/JavaEden/Clog.git'
        }
        licenses {
            license {
                name 'MIT'
                url 'https://opensource.org/licenses/mit'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'JavaEden'
                name 'Casey Brooks'
                email 'cjbrooks12@gmail.com'
            }
        }
    }

    project.publishing {
        publications {
            ClogPublication(MavenPublication) {
                from project.components.java
                artifact project.sourcesJar
                artifact project.javadocJar
                groupId "${project.group}"
                artifactId "${project.name}"
                version "${project.version}"
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', 'Simple yet powerful logging and string formatting for Java')
                    root.appendNode('name', 'Clog')
                    root.appendNode('url', 'https://javaeden.github.io/Clog')
                    root.children().last() + pomConfig
                }
            }
        }
    }

    bintray {
        user = "${project.properties['bintray_username']}"
        key = "${project.properties['bintray_apiKey']}"
        publications = ['ClogPublication']

        dryRun = project.hasProperty('dryDeploy')
        publish = !project.hasProperty('dryDeploy')
        override = true

        pkg {
            repo = "${rootProject.name}"
            name = "${project.name}"
            userOrg = 'javaeden'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/JavaEden/Clog.git'

            version {
                name = "${project.version}"
                desc = "Clog ${project.name} ${project.version}"
                released = new Date()

                gpg {
                    sign = true
                }
                mavenCentralSync {
                    sync = (project.hasProperty('includeMavenCentralSync'))
                    user = "${project.properties['mavenCentral_username']}"
                    password = "${project.properties['mavenCentral_password']}"
                }
            }
        }
    }
}

project.tasks.publish.dependsOn bintrayUpload

task deploy {
    dependsOn project.tasks.publish
    doLast { }
}